apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    sourceSets {
        String testUtilsDir = 'src/testUtils/java'
        test {
            java.srcDir testUtilsDir
        }
        androidTest {
            java.srcDir testUtilsDir
        }
    }

    defaultConfig {
        applicationId "com.example.weatherforecast"
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    productFlavors {
        full {
        }

        instrumentationTest {
            applicationId "com.example.weatherforecast.instrumentationtest"
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.1.1'
    testCompile 'org.khronos:opengl-api:gl1.1-android-2.1_r1' //Needed for robolectric to inflate relativelayout
    testCompile 'com.squareup.okhttp3:mockwebserver:3.4.1'

    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support:support-annotations:24.0.0'
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.4.1'


    compile 'com.squareup.okio:okio:1.9.0'
    compile 'com.android.support:support-annotations:24.0.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.2'
    compile 'joda-time:joda-time:2.9.4'
    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'
    compile 'org.greenrobot:eventbus:3.0.0'
}

// Get the path to ADB.  Required when running tests directly from Android Studio.
// Source: http://stackoverflow.com/a/26771087/112705
def adb = android.getAdbExe().toString()

// Source: http://stackoverflow.com/q/29908110/112705
afterEvaluate {
    task grantAnimationPermissionDev(type: Exec, dependsOn: 'installInstrumentationTestDebug') {
        doFirst {
            println("Executing: $adb shell pm grant $android.productFlavors.instrumentationTest.applicationId android.permission.SET_ANIMATION_SCALE")
            commandLine "$adb shell pm grant $android.productFlavors.instrumentationTest.applicationId android.permission.SET_ANIMATION_SCALE".split(' ')
        }
    }

    // When launching individual tests from Android Studio, it seems that only the assemble tasks
    // get called directly, not the install* versions
    tasks.each { task ->
        if (task.name.startsWith('assembleInstrumentationTestDebugAndroidTest')) {
            task.dependsOn grantAnimationPermissionDev
        }
    }
}